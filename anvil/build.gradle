buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:1.3.+'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
	}
}

apply plugin: 'com.android.library'

android {
	compileSdkVersion 'android-23'
	buildToolsVersion '22.0.1'

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	defaultConfig {
		minSdkVersion 15
		targetSdkVersion 23
	}

	lintOptions {
		abortOnError false
	}

	publishNonDefault true
	productFlavors {
		sdk10 {
			minSdkVersion 10
		}
		sdk15 {
			minSdkVersion 15
		}
		sdk19 {
			minSdkVersion 19
		}
	}
}

group = 'co.trikita'
version = '0.1.0'

apply from: 'gen.gradle'

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
	publications {
		sdk10(MavenPublication) {
			groupId project.group
			version project.version
			artifactId 'anvil-sdk10'
			artifact "$buildDir/outputs/aar/anvil-sdk10-release.aar"
		}
		sdk15(MavenPublication) {
			groupId project.group
			version project.version
			artifactId 'anvil-sdk15'
			artifact "$buildDir/outputs/aar/anvil-sdk15-release.aar"
		}
		sdk19(MavenPublication) {
			groupId project.group
			version project.version
			artifactId 'anvil-sdk19'
			artifact "$buildDir/outputs/aar/anvil-sdk19-release.aar"
		}
	}
}

bintray {
	user = hasProperty('bintrayUser') ? bintrayUser : ''
	key = hasProperty('bintrayApiKey') ? bintrayApiKey : ''
	publications = ['sdk10', 'sdk15', 'sdk19']
	pkg {
		repo = 'maven'
		name = 'anvil'
		websiteUrl = 'https://github.com/zserge/anvil'
		vcsUrl = 'https://github.com/zserge/anvil.git'
		licenses = ['MIT']
		publish = true
		version {
			name = version
			gpg {
				sign = true
				passphrase = hasProperty('bintrayGpgPassword') ? bintrayGpgPassword : ''
			}
			mavenCentralSync {
				sync = true
				user = hasProperty('bintrayOssUser') ? bintrayOssUser : ''
				password = hasProperty('bintrayOssPassword') ? bintrayOssPassword : ''
				close = '1'
			}
		}
	}
}
