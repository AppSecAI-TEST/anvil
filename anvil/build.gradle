buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:1.3.+'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
	}
}

apply plugin: 'com.android.library'

android {
	compileSdkVersion 'android-22'
	buildToolsVersion '22.0.1'

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	defaultConfig {
		minSdkVersion 10
		targetSdkVersion 22
	}

	lintOptions {
		abortOnError false
	}

	publishNonDefault true
	productFlavors {
		v19 {
			defaultConfig {
				minSdkVersion 19
				targetSdkVersion 22
			}
		}
		v15 {
			defaultConfig {
				minSdkVersion 15
				targetSdkVersion 22
			}
		}
		v10 {
			defaultConfig {
				minSdkVersion 10
				targetSdkVersion 22
			}
		}
	}
}

apply from: 'gen.gradle'

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
	publications {
		v10(MavenPublication) {
			groupId 'co.trikita'
			artifactId 'anvil-v10'
			version '0.1.0'
			artifact "$project.buildDir/outputs/aar/core-v10-release.aar"
		}
		v15(MavenPublication) {
			groupId 'co.trikita'
			artifactId 'anvil-v15'
			version '0.1.0'
			artifact "$project.buildDir/outputs/aar/core-v15-release.aar"
		}
		v19(MavenPublication) {
			groupId 'co.trikita'
			artifactId 'anvil-v19'
			version '0.1.0'
			artifact "$project.buildDir/outputs/aar/core-v19-release.aar"
		}
	}
}

bintray {
	user = hasProperty('bintrayUser') ? bintrayUser : ''
	key = hasProperty('bintrayApiKey') ? bintrayApiKey : ''
	publications = ['v10', 'v15', 'v19']
	pkg {
		repo = 'maven'
		name = 'anvil'
		websiteUrl = 'https://github.com/zserge/anvil'
		vcsUrl = 'https://github.com/zserge/anvil.git'
		licenses = ['MIT']
		publish = true
		version {
			name = '0.1.0'
			gpg {
				sign = true
				passphrase = hasProperty('bintrayGpgPassword') ? bintrayGpgPassword : ''
			}
			mavenCentralSync {
				sync = true
				user = hasProperty('bintrayOssUser') ? bintrayOssUser : ''
				password = hasProperty('bintrayOssPassword') ? bintrayOssPassword : ''
				close = '1'
			}
		}
	}
}
