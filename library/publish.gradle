def GROUPID = 'co.trikita'
def ARTIFACT = 'anvil'
def VERSION = '0.0.7'
def DESCRIPTION = 'Lightweight reactive UI library for Android'
def WEBURL = 'https://github.com/zserge/anvil'
def SCMURL = 'https://github.com/zserge/anvil.git'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = GROUPID
version = VERSION

install {
	repositories.mavenInstaller {
		pom {
			project {
				packaging 'aar'
				name ARTIFACT
				groupId GROUPID
				description DESCRIPTION
				url WEBURL
				licenses {
					license {
						name 'MIT License'
						url 'http://www.opensource.org/licenses/mit-license.php'
					}
				}
				developers {
					developer {
						id 'trikita'
						name 'Serge Zaitsev'
						email 'zaitsev.serge@gmail.com'
					}
				}
				scm {
					connection SCMURL
					developerConnection SCMURL
					url WEBURL
				}
			}
		}
	}
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}

task androidJavadocs(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: androidJavadocs) {
	classifier = 'javadoc'
	from androidJavadocs.destinationDir
}

artifacts {
	archives sourcesJar, javadocJar
}

Properties properties = new Properties()
try {
	properties.load(project.file('local.properties').newDataInputStream())
} catch (Exception e) {
	// Ignore, file may be missing
}

bintray {
	user = properties.getProperty("bintray.user")
	key = properties.getProperty("bintray.apikey")
	configurations = ['archives']
	pkg {
		repo = "maven"
		name = ARTIFACT
		websiteUrl = WEBURL
		vcsUrl = SCMURL
		licenses = ["MIT"]
		publish = true
		version {
			name = VERSION
			gpg {
				sign = true
				passphrase = properties.getProperty("bintray.gpg.password")
			}
			mavenCentralSync {
				sync = true
				user = properties.getProperty("bintray.oss.user")
				password = properties.getProperty("bintray.oss.password")
				close = '1'
			}
		}
	}
}

